#include functions/svg_drawing_mod.cpd' < !--COPIED FROM NED CODE - - > '
#defdraw_layer_h_0$(x$; y$; width$; height$)
	'<pattern id = "vLines" x="0" y="0" width="40" height="100" patternUnits="userSpaceOnUse">
	'<line x1="40" y1="0" x2="40" y2="100" style="stroke:black; stroke-width:2; stroke-opacity:1" />
	'</pattern > 
	'<rect x="'x$'" y="'y$'" width="'width$'" height="'height$'" style="stroke:black; stroke-width:2; fill:orange; fill-opacity:0.6; stroke-opacity:1" />
	'<rect x="'x$'" y="'y$'" width="'width$'" height="'height$'" fill="url(#vLines)" style="stroke:black; stroke-width:2; fill-opacity:.7; stroke-opacity:1" />
#end def
#def draw_layer_h_90$(x$; y$; width$; height$)
	'<pattern id = "hLines" x="0" y="0" width="100" height="4" patternUnits="userSpaceOnUse">
	'<line x1="0" y1="4" x2="100" y2="4" style="stroke:black; stroke-width:2; stroke-opacity:1" />
	'</pattern > 
	'<rect x="'x$'" y="'y$'" width="'width$'" height="'height$'" style="stroke:black; stroke-width:2; fill:yellow; fill-opacity:0.6; stroke-opacity:1" />
	'<rect x="'x$'" y="'y$'" width="'width$'" height="'height$'" fill="url(#hLines)" style="stroke:black; stroke-width:2; fill-opacity:.7; stroke-opacity:1" />
#end def
#def check_cltData$(cltLayers$)
	#hide
	n = n_rows(cltLayers$)
	#show
	#for layer = 1 : n
		#if cltLayers$.(layer; 2) ≠ 0 ∧ cltLayers$.(layer; 2) ≠ 90
			"ERROR: CLT LAYERS' DIRECTIONS MUST BE 0° OR 90°
			#break
		#end if
	#loop
#end def
#def draw_clt$(cltLayers$; bx$; by$; t_full$)
	#val
	#hide
	WIDTHX = 1*bx$|cm
	WIDTHY = 1*by$|cm
	HEIGHT = .5*t_full$|cm
	n = n_rows(cltLayers$)
	#show
	 '<svg width = "100"height = "100"viewBox = "0 0 100 100">  
	#for layer = 1 : n
		#if cltLayers$.(layer; 2) ≡ 0
			draw_layer_h_0$(0mm; t_cumSum0.(layer)|mm; WIDTHX; cltLayers$.(layer; 1)|mm)
			draw_layer_h_90$(WIDTHX + 500mm; t_cumSum0.(layer)|mm; WIDTHY; cltLayers$.(layer; 1)|mm)
			
		#else 
			draw_layer_h_90$(0mm; t_cumSum0.(layer)|mm; WIDTHX; cltLayers$.(layer; 1)|mm)
			draw_layer_h_0$(WIDTHX + 500mm; t_cumSum0.(layer)|mm; WIDTHY; cltLayers$.(layer; 1)|mm)
		#end if
	#loop
	'</svg>
	#equ
#end def
#def table_CLT$
	'<div style="text-align: center;">
	'<table class="bordered" style="text-align:center;width:15cm;margin: 5mm auto">
	'<thead>
	'<tr><th colspan="2">
	'Caratteristiche del pannello in CLT  
	'</th >  
	'</tr > 
	'</thead>
	#nosub
	'<tbody>
	'<tr><td>'A_x,net|cm^2'</td><td>'A_y,net|cm^2'</td></tr>
	'<tr><td>'I_x|cm^4'</td><td>'I_y|cm^4'</td></tr>
	'<tr><td>'W_x|cm^3'</td><td>'W_y|cm^3'</td></tr>
	'<tr><td>'i_x|cm'</td><td>'i_y|cm'</td></tr>
	'</tbody>
	'</table>
	'</div>
	#varsub
#end def
#def calc_cltProperties$(cltLayers$; width$; height$)
	check_cltData$(cltLayers$)
	#hide
	t_x,net = 0mm
	n = n_rows(cltLayers$)
	#for layer = 1 : n
		#if cltLayers$.(layer; 2) ≡ 0
			t_x,net = t_x,net + cltLayers$.(layer; 1)
			layer
		#end if
	#loop
	t_full = sum(col(cltLayers$; 1))
	t_y,net = t_full - t_x,net
	t_cumSum = vector(n)
	t_centreFromBottom = vector(n)
	t_centreFromMiddle = vector(n)
	t_sum = 0mm
	#for layer = 1 : n
		t_sum = t_sum + cltLayers$.(layer; 1)
		t_cumSum.(layer) = t_sum
		t_centreFromBottom.(layer) = t_cumSum.(layer) - cltLayers$.(layer; 1)/2
		t_centreFromMiddle.(layer) = t_centreFromBottom.(layer) - t_full/2
	#loop
	I_x = 0mm^4
	I_y = 0mm^4
	#for layer = 1 : n
		#if cltLayers$.(layer; 2) ≡ 0
			I_x = I_x + width$*(cltLayers$.(layer; 1)^3/12 + cltLayers$.(layer; 1)*t_centreFromMiddle.(layer)^2)
		#end if
		#if cltLayers$.(layer; 2) ≡ 90
			I_y = I_y + height$*(cltLayers$.(layer; 1)^3/12 + cltLayers$.(layer; 1)*t_centreFromMiddle.(layer)^2)
		#end if
	#loop
	W_x = I_x/(t_full/2)
	W_y = I_y/(t_full/2)
	A_full = t_full*width$
	A_x,net = t_x,net*width$
	A_y,net = t_y,net*width$
	i_x = sqrt(I_x/A_x,net)
	i_y = sqrt(I_y/A_y,net)
	t_cumSum0 = join(0mm; t_cumSum)
	#show
	table_CLT$
	draw_clt$(cltLayers$; width$; height$; t_full)
#end def